;;####################################################
;; Nader.Afshar@synchrotron.org.au 150210 Modified the PLC to use P601..P616 as the reference for amplifier mode
;;####################################################

;###########################################################
; PLC_auto_cure.pmc
;  This PLC runs PLC6 if the amplifier mode bit conflicts with the definition of the bit
;  as defined in this PLC. At time of writing (8/6/2010) sometimes the Geobrick sets the 
;  amp mode bit to servo for no reason, this PLC attempts to detect and fix the problem.
;  Delta Tau have promised a firmware update to fix this problem.
;  This PLC is only for use with Geobricks which have the new amplifier firmware
;  (look for a sticker saying flashed 09/09/09 or some other time in 09/09, if
;   you have firmware version 1.946 firmware or later you probably have the new 
;   amplifier firmware too). 
;  This PLC is derived from Trevors PLC7_Motion_Controls_Auto_Kill_or_Cure.pmc
; 
; NB Needs either BRICK_M_variables.pmc or PMAC_M_variables.pmc loaded (for M5006 defn.)
;
; Variables:
; P999
;   The number of axes on the controller 1..16 (8 for a standard geobrick)
;;	uses:
;;  P601..P616
;;   Define which axis are steppers - set to 1 for a stepper, 0 for servo
;###########################################################

; P601..P616 must have been already defined by PLC 6 - 1 for a stepper, 0 for servo

; number of axes
#define naxis                 P999

; used for storing current axis number
#define axis_number           P900

; P601..P616 must have been already defined by PLC 6 - 1 for a stepper, 0 for servo
#define amplifier_mode        P(axis_number + 600)

#define error_count           P(axis_number + 916)

#define plc6_flag             M5006

; define amp mode fault counter (incremented each time PLC 6 is run - cleared manually)
#define fault_counter         P(axis_number + 932)

; amplifier mode bit Mxx89
#define amp_mode_bit          M(axis_number*100 + 89)

#define kill_axis             COMMAND"K"

; this is how many matching readings are needed for amplifier status
#define NREADINGS 5

CLOSE
OPEN PLC 9
CLEAR

axis_number = 1
WHILE(axis_number !> naxis)
	IF(plc6_flag=1) ;if plc6 is not running
		IF(amp_mode_bit=0)  ;this says it is a  Brushless or dc servo
		AND(amplifier_mode=1) ;if configured as stepper we have a problem...
			error_count=error_count+1 ;debounce
			IF (error_count !< NREADINGS)
				ADDRESS#axis_number
				kill_axis
				fault_counter=fault_counter+1
				ENABLE PLC6   ;reset Mode
				error_count=0
			ENDIF
		ELSE
			error_count=0
		END IF

		axis_number = axis_number+1
	END IF
ENDWHILE

CLOSE
